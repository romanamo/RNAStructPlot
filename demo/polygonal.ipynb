{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6003f16a",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Graphs\n",
    "using RNAStructPlot\n",
    "using CairoMakie\n",
    "\n",
    "sequence = \"CGCUUCAUAUAAUCCUAAUGAUAUGGUUUGGGAGUUUCUACAUUAUGAAGUG\"\n",
    "notation = \"...(((((((..((((((.........))))))......)...))))))...\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "970dc2ff",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "RegularPolygon\n",
    "\n",
    "a(TYPEDFIELDS)\n",
    "\"\"\"\n",
    "struct RegularPolygon\n",
    "    \"radius of the polygon circle\"\n",
    "    radius::Float64\n",
    "    \"amount of points\"\n",
    "    n::Int64\n",
    "    \"center of polygon\"\n",
    "    center::Vector{Float64}\n",
    "    \"rotation of the polygon\"\n",
    "    rotation::Float64\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "a(SIGNATURES)\n",
    "\n",
    "Constructs a `RegularPolygon` with side lengths of 1 unit.\n",
    "\"\"\"\n",
    "function RegularPolygonSides(n::Int, center::Vector{Float64}=[0.0,0.0], rotation::Float64=0.0, sides::Float64=1.0)::RegularPolygon\n",
    "    radius = sides/(2*sin(pi/n))\n",
    "    return RegularPolygon(radius, n, center, rotation)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a05efd3d",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "a(SIGNATURES)\n",
    "\n",
    "Gets an ordered list of points for the polygon.\n",
    "\"\"\"\n",
    "function points(polygon::RegularPolygon)::Vector{Vector{Float64}}\n",
    "    segmentsize = 2*pi/polygon.n\n",
    "    p = []\n",
    "    for i=0:(polygon.n-1)\n",
    "        # caculate angle of point relative to center\n",
    "        current = segmentsize*i+polygon.rotation\n",
    "        # convert polar to cartesian coordinates, since radius and angle are known\n",
    "        px, py = polygon.radius .* [cos(current), sin(current)]\n",
    "        # append absolute position by adding center coordinates\n",
    "        push!(p, [px, py]+polygon.center)\n",
    "    end\n",
    "    return p\n",
    "end\n",
    "\n",
    "function find_pairing((p1, p2), pairings)\n",
    "    for (k, v) in pairings\n",
    "        if (p1, p2) in v\n",
    "            return k, v\n",
    "        end\n",
    "    end\n",
    "    return missing\n",
    "end\n",
    "\n",
    "normalizeangle(θ) = θ < 0 ? θ + 2*pi : θ"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "98b7a927",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "\n",
    "function drawpolygonal(rnatree::RNATreeGraph, rnabase::RNABaseGraph;sidelength::Float64=1.0,stemlength::Float64=0.5)\n",
    "\n",
    "    loops = sort(map(sort, collect(values(rnatree.loopbases)));by=minimum)\n",
    "    stem = rnatree.regionpairs\n",
    "\n",
    "    coords = Dict()\n",
    "    numberings = Dict()\n",
    "    \n",
    "    start = minimum(vertices(rnatree.graph))\n",
    "\n",
    "    function layoutloop(treevertex, polygon)\n",
    "        loop = loops[treevertex]\n",
    "        loopsize = length(loop)\n",
    "        polygonpoints = points(polygon)\n",
    "\n",
    "        # set base pair in loop to corner in polygon\n",
    "        for (i, v) in enumerate(loop)\n",
    "            coords[v] = polygonpoints[i]\n",
    "            # only write numbering if not already defined in region stem\n",
    "            if !haskey(numberings, v)\n",
    "                numberings[v] = coords[v] + normalize(coords[v] - polygon.center)*0.5\n",
    "            end\n",
    "        end\n",
    "\n",
    "        # loop through possible pairs attached to loop \n",
    "        for i in range(0, loopsize-1)\n",
    "            current = loop[i%loopsize+1]\n",
    "            next = loop[(i+1)%loopsize+1]\n",
    "\n",
    "            # check if pair is the beginning of region stem, if yes draw it\n",
    "            if hasexactpair(rnatree, current, next)\n",
    "                (from, to), stempairs = find_pairing((current, next), stem)\n",
    "                layoutstem(from, to, current, next, stempairs, polygon)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    function layoutstem(fromvertex, tovertex, current, next, stempairs, frompolygon)\n",
    "        middle = coords[current]+(coords[next]-coords[current])/2\n",
    "        direction = normalize(middle - frompolygon.center)*stemlength\n",
    "\n",
    "        # walk along region stempairs and calculate their positions\n",
    "        for (i, (low, high)) in enumerate(stempairs)\n",
    "            coords[low]  = coords[current] + direction*(i-1)\n",
    "            coords[high] = coords[next] + direction*(i-1)\n",
    "\n",
    "            numberings[high] = coords[high] + (coords[high] - coords[low])/2\n",
    "            numberings[low] = coords[low] + (coords[low] - coords[high])/2\n",
    "        end\n",
    "\n",
    "        # prepare next loop attached to region stem\n",
    "        toloop = loops[tovertex]\n",
    "        toloopsize = length(toloop)\n",
    "\n",
    "        # calculate dimension of polygon\n",
    "        alpha = 2*pi/toloopsize\n",
    "        tomid = middle + direction*(length(stempairs)-1)\n",
    "        toscale = sidelength/(2*tan(alpha/2))\n",
    "        tocenter = tomid + toscale*normalize(tomid - frompolygon.center)\n",
    "\n",
    "        # determine starting angle\n",
    "        ox, oy = coords[last(stempairs)[1]] .- tocenter\n",
    "        torotation = normalizeangle(atan(oy, ox))\n",
    "\n",
    "        topolygon = RegularPolygonSides(toloopsize, tocenter, torotation, sidelength)\n",
    "\n",
    "        # recursively go down to next subtree\n",
    "        layoutloop(tovertex, topolygon)\n",
    "    end\n",
    "\n",
    "    layoutloop(start, RegularPolygonSides(length(loops[start]), [0.,0.], 0. , sidelength))\n",
    "    return DrawResult(coords, numberings)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "96910909",
   "metadata": {},
   "outputs": [],
   "source": [
    "using RNAStructPlot\n",
    "\n",
    "rnatree = dotbrackettree(sequence, notation)\n",
    "rnabase = dotbracketbase(sequence, notation)\n",
    "\n",
    "result = drawpolygonal(rnatree, rnabase)\n",
    "\n",
    "drawgeneric(rnabase, result)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
